plugins {
    id "com.jfrog.bintray" version "1.7"
    id 'org.asciidoctor.gradle.asciidoctor' version '1.5.1'
    id "nebula.provided-base" version "3.1.0"
    id 'org.ajoberstar.github-pages' version '1.1.0'
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'codenarc'
apply plugin: "jacoco"


group 'com.jameskleeh'
version '0.1.0.BUILD-SNAPSHOT'
targetCompatibility = 1.7

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    provided 'org.codehaus.groovy:groovy-all:2.4.7'
    compile "org.apache.poi:poi-ooxml:3.14"
    codenarc 'org.codenarc:CodeNarc:0.25.2'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testRuntime "org.slf4j:slf4j-api:1.7.10"
}

project.ext {
    githubUrl = 'https://github.com/jameskleeh/groovy-excel-builder'
    artifactId = 'excel-builder'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['maven']
    pkg {
        repo = 'com.jameskleeh'
        name = 'excel-builder'
        licenses = ['Apache-2.0']
        vcsUrl = project.githubUrl
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId project.artifactId
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.artifactId
                    description project.description
                    url project.githubUrl
                    scm {
                        url project.githubUrl
                        connection "scm:${project.githubUrl}.git"
                        developerConnection "scm:${project.githubUrl}.git"
                    }
                    licenses {
                        license {
                            name 'Apache License, Version 2.0'
                            url 'https://www.apache.org/licenses/LICENSE-2.0'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'jameskleeh'
                            name 'James Kleeh'
                            email 'james.kleeh@gmail.com'
                        }
                    }
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

asciidoctor {
    sourceDir = file('docs')

    resources {
        from(sourceDir) {
            include 'css/**', 'images/**'
        }
    }

    attributes 'source-highlighter': 'prettify',
            'docinfo1': ['version': project.version],
            'imagesdir': 'images',
            'stylesdir': 'css',
            icons: 'font',
            'toc': 'left',
            version: project.version,
            'projectUrl': project.githubUrl
}

githubPages {
    repoUri = "${project.githubUrl}.git"

    credentials {
        username = githubKey
        password = ''
    }

    pages {
        from file(asciidoctor.outputDir.path + '/html5')
    }
}

publishGhPages.dependsOn asciidoctor

codenarc {
    toolVersion = '0.25.2'
    configFile = file("${rootProject.projectDir}/config/codenarc/rules.groovy")
}

test {
    finalizedBy jacocoTestReport
}
